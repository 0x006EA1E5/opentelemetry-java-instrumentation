buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.franzbecker:gradle-lombok:1.13"
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.2"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.13"
    classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.5'
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.13'
  id 'com.github.sherter.google-java-format' version '0.6'
}

def isCI = System.getenv("CI") != null

allprojects {
  group = 'com.datadoghq'
  version = '0.7.1-SNAPSHOT'

  if (isCI) {
    buildDir = "${rootDir}/workspace/${projectDir.path.replace(rootDir.path, '')}/build/"
  }

  apply from: "${rootDir}/gradle/dependencies.gradle"
}

repositories {
  jcenter()
  mavenCentral()
}

description = 'dd-trace-java'

task traceAgentTest {}
task latestDepTest {}
task latestDepTestJava9 {}

// Applied here to allow publishing of artifactory build info
apply from: "${rootDir}/gradle/publish.gradle"

buildScan {
  termsOfServiceUrl = 'https://gradle.com/terms-of-service'
  termsOfServiceAgree = 'yes'

  if (isCI) {
    publishAlways()
    tag 'CI'
  }
}

ext.gradleWrapperVersion = '4.6'
task wrapper(type: Wrapper) {
  gradleVersion = gradleWrapperVersion
}
