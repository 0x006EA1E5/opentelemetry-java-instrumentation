# Instrument Mongo client
# ========================
RULE mongo
CLASS com.mongodb.MongoClientOptions$Builder
METHOD build
HELPER io.opentracing.contrib.agent.helper.MongoHelper
AT ENTRY
IF TRUE
DO
  patch($this);
ENDRULE


# Instrument AWS SDK client
# ==========================
RULE awsclient
CLASS ^com.amazonaws.client.builder.AwsClientBuilder
METHOD build()
HELPER io.opentracing.contrib.agent.helper.AWSClientHelper
AT ENTRY
IF TRUE
DO
  patch($this);
ENDRULE

# Instrument Apache HTTP Client
# =============================
RULE apache http
CLASS org.apache.http.impl.client.HttpClientBuilder
METHOD create()
HELPER io.opentracing.contrib.agent.helper.ApacheHTTPClientHelper
AT EXIT
IF TRUE
DO
  $! = patch($!);
ENDRULE

# Instrument Elasticsearch Transport Client
# ==========================================
RULE elasticsearch
INTERFACE org.elasticsearch.client.ElasticsearchClient
METHOD execute(org.elasticsearch.action.Action,org.elasticsearch.action.ActionRequest,org.elasticsearch.action.ActionListener)
HELPER io.opentracing.contrib.agent.helper.ElasticsearchHelper
AT ENTRY
IF $# == 3 AND NOT $3.getClass().getCanonicalName().equals("io.opentracing.contrib.elasticsearch.TracingResponseListener")
DO
  registerArgs($1);
  $3 = patch($3);
ENDRULE

# Instrument Cassandra client
# ===========================
RULE cassandra
CLASS com.datastax.driver.core.Cluster$Manager
METHOD newSession()
HELPER io.opentracing.contrib.agent.helper.CassandraHelper
AT EXIT
IF TRUE
DO
   $! = patch($!);
ENDRULE



# Instrument Jetty
# ===========================
# State 0 - no filter installed
# State 1 - filter installed
RULE jetty
CLASS org.eclipse.jetty.servlet.ServletContextHandler
METHOD <init>
HELPER io.opentracing.contrib.agent.helper.JettyServletHelper
AT EXIT
IF getState($0.getServletContext()) == 0
DO
    patch($this)
ENDRULE


# Instrument Tomcat
# ===========================
RULE tomcat
CLASS org.apache.catalina.core.ApplicationContext
METHOD <init>
HELPER io.opentracing.contrib.agent.helper.TomcatServletHelper
AT EXIT
IF TRUE
DO
    patch($this)
ENDRULE



# Instrument OkHttp
# ===========================
RULE okhttp
CLASS okhttp3.OkHttpClient$Builder
METHOD build()
HELPER io.opentracing.contrib.agent.helper.OkHttpHelper
AT EXIT
IF TRUE
DO
    patch($this)
ENDRULE
