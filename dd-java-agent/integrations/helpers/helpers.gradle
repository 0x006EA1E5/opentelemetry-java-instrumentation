apply plugin: "com.github.johnrengelman.shadow"
apply from: "${rootDir}/gradle/java.gradle"

// We want to keep this jar as lean as possible. Only helpers and OT contrib classes.
configurations.compile {
  transitive = false
}

dependencies {
  compileOnly project(':dd-trace')
  compileOnly project(':dd-trace-annotations')
  compileOnly deps.slf4j

  compileOnly group: 'org.jboss.byteman', name: 'byteman', version: '4.0.0-BETA5'

  compile group: 'io.opentracing.contrib', name: 'opentracing-web-servlet-filter', version: '0.0.9'
  compile group: 'io.opentracing.contrib', name: 'opentracing-okhttp3', version: '0.0.5'
  compile group: 'io.opentracing.contrib', name: 'opentracing-jms-common', version: '0.0.3'
  compile group: 'io.opentracing.contrib', name: 'opentracing-jms-2', version: '0.0.3'
  compile group: 'io.opentracing.contrib', name: 'opentracing-aws-sdk', version: '0.0.2'
  compile group: 'io.opentracing.contrib', name: 'opentracing-cassandra-driver', version: '0.0.2'
  compile group: 'io.opentracing.contrib', name: 'opentracing-apache-httpclient', version: '0.0.2'

  compileOnly group: 'org.mongodb', name: 'mongo-java-driver', version: '3.4.2'
  compileOnly group: 'org.mongodb', name: 'mongodb-driver-async', version: '3.4.2'
  compileOnly group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  compileOnly group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'
  compileOnly group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.2.0'
  compileOnly group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
}

jar {
  classifier = 'unbundled'
}

shadowJar {
  classifier null
  configurations = [project.configurations.compile]

  relocate 'org.slf4j', 'dd.slf4j'  // Prevents conflict with other SLF4J instances. Important for premain.

  if (!project.hasProperty("disableShadowRelocate") || !disableShadowRelocate) {
    // This is needed to align with the relocation of TraceResolver in dd-java-agent.
    relocate 'io.opentracing.contrib', 'dd.opentracing.contrib'
  }

  dependencies {
    exclude(dependency('org.projectlombok:lombok:1.16.18'))
  }
}
