apply plugin: 'version-scan'

versionScan {
  group = "io.ratpack"
  module = 'ratpack'
  versions = "[1.4.0,)"
  verifyPresent = [
    "ratpack.path.PathBinding": "getDescription",
  ]
}

if (JavaVersion.current().java8Compatible) {
  apply from: "${rootDir}/gradle/java.gradle"

  // Ratpack only supports Java 1.8+
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
  dependencies {
    compileOnly group: 'io.ratpack', name: 'ratpack-core', version: '1.4.0'

    compile project(':dd-trace-ot')
    compile project(':dd-java-agent:agent-tooling')

    compile deps.bytebuddy
    compile deps.opentracing
    compile deps.autoservice

    testCompile project(':dd-java-agent:testing')
    testCompile group: 'io.ratpack', name: 'ratpack-test', version: '1.4.0'

    testCompile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
  }
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
  sourceSets {
    test {
      groovy {
        // These classes use Ratpack which requires Java 8. (Currently also incompatible with Java 9.)
        exclude '**/RatpackTest.groovy'
      }
    }
  }
}

