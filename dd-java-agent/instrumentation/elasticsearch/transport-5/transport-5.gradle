// Set properties before any plugins get loaded
ext {
  minJavaVersionForTests = JavaVersion.VERSION_1_8
}

muzzle {
  pass {
    group = "org.elasticsearch.client"
    module = "transport"
    versions = "[5.0.0,5.3.0)"
    assertInverse = true
  }
  pass {
    group = "org.elasticsearch"
    module = "elasticsearch"
    versions = "[5.0.0,5.3.0)"
    assertInverse = true
  }
}

apply from: "${rootDir}/gradle/java.gradle"

apply plugin: 'org.unbroken-dome.test-sets'

testSets {
  latestDepTest {
    dirName = 'test'
  }
}

dependencies {
  compileOnly group: 'org.elasticsearch.client', name: 'transport', version: '5.0.0'

  compile project(':dd-java-agent:agent-tooling')
  compile project(':dd-java-agent:instrumentation:elasticsearch')

  compile deps.bytebuddy
  compile deps.opentracing
  annotationProcessor deps.autoservice
  implementation deps.autoservice

  testCompile project(':dd-java-agent:testing')
  // Ensure no cross interference
  testCompile project(':dd-java-agent:instrumentation:elasticsearch:rest-5')
  // Include httpclient instrumentation for testing because it is a dependency for elasticsearch-rest-client.
  // It doesn't actually work though.  They use HttpAsyncClient, which isn't currently instrumented.
  // TODO: add HttpAsyncClient instrumentation when that is complete.
  testCompile project(':dd-java-agent:instrumentation:apache-httpclient-4')
  // TODO: add netty instrumentation when that is complete.

  testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
  testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'

  testCompile group: 'org.elasticsearch.plugin', name: 'transport-netty3-client', version: '5.0.0'
  testCompile group: 'org.elasticsearch.client', name: 'transport', version: '5.0.0'

  latestDepTestCompile group: 'org.elasticsearch.plugin', name: 'transport-netty3-client', version: '5.2.+'
  latestDepTestCompile group: 'org.elasticsearch.client', name: 'transport', version: '5.2.+'
}
